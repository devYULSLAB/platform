plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
}

allprojects {
    group = 'com.platform'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    // Apply configurations only to projects that have the Java plugin
    plugins.withType(JavaPlugin) {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(24)
            }
        }

        dependencies {
            // Force Lombok version compatible with Java 24
            compileOnly 'org.projectlombok:lombok:1.18.38'
            annotationProcessor 'org.projectlombok:lombok:1.18.38'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
        }

        tasks.named('test') {
            useJUnitPlatform()
        }
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }
}
